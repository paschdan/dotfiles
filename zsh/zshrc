emulate zsh


if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
	  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

typeset -gaU cdpath fpath mailpath path
path=($HOME/bin $HOME/.local/bin $path)

function jit() {
	emulate -L zsh
	[[ ${(%):-%#} == '#' || $1.zwc -nt $1 || ! -w ${1:h} ]] || zcompile $1
}

function jit-source() {
	emulate -L zsh
	[[ -e $1 ]] || return
	jit $1
	source $1
}

jit ~/.zshrc

#completions
fpath=(~/dotfiles/zsh/completions $fpath)

autoload -Uz compinit && compinit

#history settings

jit-source ~/dotfiles/zsh/history.zsh

#aliases
jit-source ~/dotfiles/zsh/aliases/docker.zsh
jit-source ~/dotfiles/zsh/aliases/git.zsh

# fzf
path+=~/dotfiles/zsh/fzf/bin
FZF_COMPLETION_TRIGGER=
export FZF_DEFAULT_COMMAND='rg --files --hidden'

# bindings

# Shows '...' while completing. No `emulate -L zsh` to pick up dotglob if it's set.
if (( ${+terminfo[rmam]} && ${+terminfo[smam]} )); then
  function expand-or-complete-with-dots() {
    echo -nE - ${terminfo[rmam]}${(%):-"%F{red}......%f"}${terminfo[smam]}
    zle expand-or-complete
    zle redisplay
  }
else
  function expand-or-complete-with-dots() {
    zle expand-or-complete
  }
fi

# Similar to fzf-history-widget. Extras:
#
#   - `awk` to remove duplicate
#   - preview pane with syntax highlighting
function fzf-history-widget-unique() {
  local selected
  setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
  local preview='echo -E {} | cut -c8- | xargs -0 echo -e | bat -l bash --color always -pp'
  selected="$(
    fc -rl 1 |
    awk '!_[substr($0, 8)]++' |
    $(__fzfcmd) +m -n2..,.. --tiebreak=index --height=80% --preview-window=down:50% \
      --query=$LBUFFER --preview=$preview )"
  local ret=$?
  [[ -n "$selected" ]] && zle vi-fetch-history -n $selected
  zle .reset-prompt
  return $ret
}

zle -N expand-or-complete-with-dots
zle -N fzf-history-widget-unique

fzf_default_completion=expand-or-complete-with-dots

function bindkey() {}
jit-source ~/dotfiles/zsh/fzf/shell/completion.zsh
jit-source ~/dotfiles/zsh/fzf/shell/key-bindings.zsh
unfunction bindkey

bindkey '\t'      expand-or-complete-with-dots        # tab        completion with '...'
bindkey '\e[1;3B' fzf-cd-widget                       # alt+down   fzf cd
bindkey '^T'      fzf-completion                      # ctrl+t     fzf completion
bindkey '^R'      fzf-history-widget-unique           # ctrl+r     fzf history



# Theme
jit-source ~/.p10k.zsh
jit-source ~/dotfiles/zsh/powerlevel10k/powerlevel10k.zsh-theme

# highlight
jit-source ~/dotfiles/zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.plugin.zsh

#Substring Search
jit-source ~/dotfiles/zsh/zsh-history-substring-search/zsh-history-substring-search.zsh

bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down
